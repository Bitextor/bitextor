import gzip
import lzma
import tldextract

from tld import get_tld
from tqdm import tqdm
from collections import defaultdict
from contextlib import contextmanager
from pathlib import Path

###########################################################
# UTILS 

@contextmanager
def open_gzip_or_plain(file_path):

    def decode_text(file_handler):
        for line in file_handler:
            yield line.decode('utf-8')

    f = None
    try:
        #print("file_path", file_path)
        if file_path[-3:] == ".gz":
            f = gzip.open(file_path, 'rb')
            yield decode_text(f)
        elif file_path[-3:] == ".xz":
            f = lzma.open(file_path, 'rb')
            yield decode_text(f)
        else:
            f = open(file_path, 'r')
            yield f

    except Exception:
        raise Exception("Error occured while loading a file {}".format(file_path))

    finally:
        if f:
            f.close()


###########################################################

BITEXTOR=config["bitextor"]

LANG1=config["lang1"]
LANG2=config["lang2"]
MODEL='share/bitextor/model/keras.model'
WEIGHTS='share/bitextor/model/keras.weights'
TMPDIR=config["temp"]
MINQUALITY=config["minquality"]
MAXLINES=config["maxlines"]

#Working paths
permanent=config["permanent-dir"]
transient=config["transient-dir"]

#Dictionary
if "dic" in config:
  DIC=config["dic"]
else:
  DIC=None

#Option to use the NLTK tokenizer
if "nltk" in config and (config["nltk"]==True):
  USENLTK='--nltk'
else:
  USENLTK=''

#Option to ignore Boilerpipie: if the option is enabled, boilerpipe is not used
if "ignore-boilerpipe-cleaning" in config and config["ignore-boilerpipe-cleaning"]==True:
  IGNOREBOILER="1"
else:
  IGNOREBOILER="0"

#Option to use the JHU-pipeline pre-processing tool; it can be only used with HTTrack
if "jhu-lett" in config and config["jhu-lett"]==True:
  LETT="jlett"
else:
  LETT="lett"

#Option to use HTTrack for crawling instead of the native bitextor crawler
if "httrack" in config and config["httrack"]==True:
  CRAWLTARGET="httrack"
else:
  CRAWLTARGET="creepy"

############ OPTIONS FOR THE NATIVE BITEXTOR CRAWLER ############

#If this option is enabled the crawler will keep crawling across a whole top-level domain (.es, .com, .fr, etc.)
if "crawl-tld" in config and config["crawl-tld"]==True:
  TLD_CRAWL="-D"
else:
  TLD_CRAWL=""

#If this option is enabled, a size-limit is set for crawled data (for example "size-limit": "1G")
if "size-limit" in config:
  CRAWLSIZELIMIT="-s "+config["size-limit"]
else:
  CRAWLSIZELIMIT=""

#If this option is enabled, a time-limit is set for crawling data (for example "time-limit": "1h")
if "time-limit" in config:
  CRAWLTIMELIMIT="-t "+config["time-limit"]
else:
  CRAWLTIMELIMIT=""

#Option to set how many threads will be used for crawling (default value: 2). Note that too many threads can cause the server hosting the website to reject some of the simultaneous connections.
if "crawler-num-threads" in config:
  CRAWLJOBS="-j "+str(config["crawler-num-threads"])
else:
  CRAWLJOBS="-j 2"

#Connection timeout in the crawler
if "timeout-crawl" in config:
  CRAWLTIMEOUT="-o "+config["timeout-crawl"]
else:
  CRAWLTIMEOUT=""

#If this option is set, the "crawler" object will be dump as a pickle, so crawling can be continued afterwards
if "write-crawling-file" in config:
  CRAWLDUMPARGS="-d "+config["write-crawling-file"]
else:
  CRAWLDUMPARGS=""

#If this option is set, crawling will be continued from the pickle object dumped in a previous crawl
if "continue-crawling-file" in config:
  CONTINUECRAWL="-l "+config["continue-crawling-file"]
else:
  CONTINUECRAWL=""


############ OPTIONS FOR THE MALIGN ALIGNER ############

if "paracrawl-aligner-command" in config:
  MT_COMMAND=config["paracrawl-aligner-command"]
  DOCALIGNEXT="paracrawl"
else:
  MT_COMMAND=""
  DOCALIGNEXT="bitextor"

############ FILTERING AND POST-PROCESSING OPTIONS ############

if "bicleaner" in config:
  BICLEANEROPTION=",bicleaner"
  BICLEANER="bicleaner"
  BICLEANER_CONFIG=config["bicleaner"]
else:
  BICLEANEROPTION=""
  BICLEANER="segclean"

if "bicleaner-threshold" in config:
  BICLEANER_THRESHOLD=config["bicleaner-threshold"]
else:
  BICLEANER_THRESHOLD=0.0

if "elrc" in config and config["elrc"]==True:
  ELRCSCORES="elrc"
else:
  ELRCSCORES=BICLEANER

#========================= MAPPING URLS AND OUTPUT FILES =========================#

def createDomainMap(urls):
    domains={}
    for url in urls:
        domain = tldextract.extract(url).domain
        if domain not in domains:
            domains[domain]=[]
        domains[domain].append(url)
        #print("subdomain", domain, url)
    return domains

def loadDomains(file_path):
    domains = set()
    with file_path.open("r") as f:
        for line in f:
            line = line.strip()
            if len(line):
                domains.add(line)

    return domains

def getFullDomainsFromLangstat(langstat_path, lang1, lang2, threshold, exclude_path):
    print("langstat_path", langstat_path, file=sys.stderr)
    l12 = [lang1.lower(), lang2.lower()]
    domains = defaultdict(dict)

    excluded_set = set()
    if exclude_path:
        excluded_set = loadDomains(Path(exclude_path))

    sys.stderr.write(
        "Gathering domain information for {0} and {1}...\n".format(*l12))
    with tqdm(total=None) as pbar:
        with open_gzip_or_plain(langstat_path) as f:
            for line in f:
                split_line = line.strip().split()
                if len(split_line) != 3:
                    continue

                domain, lang, byte_len = split_line
                if lang.lower() in l12:
                    domains[domain][lang.lower()] = byte_len

                pbar.update(1)
    #print("Loaded ", langstat_path, file=sys.stderr)

    domains_to_crawl = []
    for k, v in domains.items():
        if tldextract.extract(k).domain in excluded_set:
            continue

        if len(v) == 2:
            lang1_bytes = int(v[l12[0]])
            lang2_bytes = int(v[l12[1]])
            if lang1_bytes >= int(threshold) and lang2_bytes >= int(threshold):
                #print("k", k)
                domains_to_crawl.append(k)
    print(len(domains_to_crawl), " domains found", file=sys.stderr)
    return domains_to_crawl

if "domains" in config:
    print("domains given")
    domains = config["domains"]
    print("domains", type(domains))
elif "langstat" in config:
    print("find domains in langstat")
    langstat_path = config["langstat"]
    lang1 = config["lang1"]
    lang2 = config["lang2"]
    threshold = config["langstat-threshold"]
    exclude_path = config["langstat-exclude-domains"]

    domains = getFullDomainsFromLangstat(langstat_path, lang1, lang2, threshold, exclude_path)
else:
    print("Need list of urls or langstat file")
    exit()

domain_2_fulldomains = createDomainMap(domains)
print("domain_2_fulldomains", domain_2_fulldomains)


evaluation="{dir}/evaluation".format(dir=config["nmt-dir"])

include:"Snakefile.mt"


#================================== TARGET FILES ==================================#
rule all:
    input:
        "{dir}/report".format(dir=evaluation)
#        expand("{dir}/{l1}-{l2}.tmx", dir=permanent, l1=LANG1, l2=LANG2)

#================================== PREPROCESSING ==================================#
#"http://www.elenacaffe1863.com/", "http://elenacaffe1863.com/", "http://vade-retro.fr"
rule creepy_download:
    params:
        url="http://{target}"
    output:
        '{dir}'.format(dir=permanent)+'/{target}.creepy.warc'
    shell:
        'echo {params.url}'
        'mkdir -p {permanent}; '
        'python3 {BITEXTOR}/bin/bitextor-crawl {TLD_CRAWL} {CRAWLSIZELIMIT} {CRAWLTIMELIMIT} {CRAWLJOBS} {CRAWLTIMEOUT} {CRAWLDUMPARGS} {CONTINUECRAWL} {params.url} > {output}'

rule httrack_download:
    output:
        '{dir}'.format(dir=permanent)+'/{target}.httrack.warc'
    params:
        url="http://{target}"
    shell:
        'mkdir -p {permanent}; '
        'DIRNAME=$(mktemp -d {TMPDIR}/downloaded_websites.XXXXXX); '
        '{BITEXTOR}/bin/bitextor-downloadweb {params.url} $DIRNAME; '
        '{BITEXTOR}/bin/bitextor-webdir2warc $DIRNAME > {output}; '
        'rm -rf $DIRNAME;'

rule concat_subdomains:
    input:
        lambda w: expand('{dir}/{subdomain}.{crawler}.warc', dir=permanent, subdomain=domain_2_fulldomains[w.target], crawler=CRAWLTARGET)
    output:
        "{dir}".format(dir=transient)+"/{target}.warc.concat"
    shell:
        'cat {input} > {output}'

rule warc2tt:
    input:
        '{target}.warc.concat'
    output:
        '{target}.tt'
    shell:
        '{BITEXTOR}/bin/bitextor-warc2tt < {input} > {output}'

rule tt2ttmime:
    input:
        '{target}.tt'
    output:
        '{target}.ttmime'
    shell:
        '{BITEXTOR}/bin/bitextor-identifyMIME < {input} > {output}'

rule tt2xtt:
    input:
        #If HTTRACK is enalbed, httrack rule is run; otherwise, download rule is applied
        '{target}.ttmime'
    output:
        '{target}.xtt'
    shell:
        'java -Dfile.encoding=UTF-8 -jar {BITEXTOR}/share/java/piped-tika.jar -x < {input} > {output}'

rule xtt2boiler:
    input:
        '{target}.xtt'
    output:
        '{target}.boiler'
    shell:
        "if [ \"{IGNOREBOILER}\" == \"1\" ]; then "
        "  java -Dfile.encoding=UTF-8 -jar {BITEXTOR}/share/java/piped-boilerpipe.jar < {input} > {output}; "
        "else "
        "  cat {input} > {output}; "
        "fi"

rule boiler2ett:
    input:
        '{target}.boiler'
    output:
        '{target}.ett'
    shell:
        '{BITEXTOR}/bin/bitextor-dedup < {input} > {output}'

rule ett2tika:
    input:
        '{target}.ett'
    output:
        '{target}.tika'
    shell:
        'java -Dfile.encoding=UTF-8 -jar {BITEXTOR}/share/java/piped-tika.jar -t < {input} > {output}'

rule ett2lett:
    input:
        '{target}.tika'
    output:
        '{target}.lett'
    shell:
        'bash {BITEXTOR}/bin/bitextor-ett2lett -l {LANG1},{LANG2} < {input} > {output}'

rule httrack2lett:
    output:
        '{target}.jlett'
    input:
        '{target}.tar'
    shell:
        '{BITEXTOR}/bin/tar2lett {input} {LANG1} {LANG2} > {output}'

rule lett2lettr:
    input:
        expand("{{target}}.{extension}", extension=LETT)
    output:
        '{target}.lettr'
    shell:
        '{BITEXTOR}/bin/bitextor-lett2lettr < {input} > {output}'

rule lettr2idx:
    input:
        '{target}.lettr'
    output:
        '{target}.idx'
    shell:
        '{BITEXTOR}/bin/bitextor-lett2idx  --lang1 {LANG1} --lang2 {LANG2} -m 15 < {input} > {output}'




#================================== DOCUMENT ALIGNMENT ==================================#

rule idx2ridx_l1tol2:
    input:
        '{target}.idx'
    output:
        '{target}.1.ridx'
    shell:
        '{BITEXTOR}/bin/bitextor-idx2ridx -d {DIC} --lang1 {LANG1} --lang2 {LANG2} < {input} > {output}'

rule idx2ridx_l2tol1:
    input:
        '{target}.idx'
    output:
        '{target}.2.ridx'
    shell:
        '{BITEXTOR}/bin/bitextor-idx2ridx -d {DIC} --lang1 {LANG2} --lang2 {LANG1}  < {input} > {output}'

rule ridx2imagesetoverlap:
    input:
        '{target}.{num}.ridx',
        '{target}.lettr'
    output:
        '{target}.{num}.imgoverlap'
    shell:
        '{BITEXTOR}/bin/bitextor-imagesetoverlap -l {wildcards.target}.lettr < {input[0]} > {output}'

rule imagesetoverlap2structuredistance:
    input:
        '{target}.{num}.imgoverlap',
        '{target}.lettr'
    output:
        '{target}.{num}.structuredistance'
    shell:
        '{BITEXTOR}/bin/bitextor-structuredistance -l {wildcards.target}.lettr < {input[0]} > {output}'

rule structuredistance2urldistance:
    input:
        '{target}.{num}.structuredistance',
        '{target}.lettr'
    output:
        '{target}.{num}.urldistance'
    shell:
        '{BITEXTOR}/bin/bitextor-urlsdistance -l {wildcards.target}.lettr < {input[0]} > {output}'

rule urldistance2mutuallylinked:
    input:
        '{target}.{num}.urldistance',
        '{target}.lettr'
    output:
        '{target}.{num}.mutuallylinked'
    shell:
        '{BITEXTOR}/bin/bitextor-mutuallylinked -l {wildcards.target}.lettr < {input[0]} > {output}'

rule mutuallylinked2urlscomparison:
    input:
        '{target}.{num}.mutuallylinked',
        '{target}.lettr'
    output:
        '{target}.{num}.urlscomparison'
    shell:
        '{BITEXTOR}/bin/bitextor-urlscomparison -l {wildcards.target}.lettr < {input[0]} > {output}'

rule urlscomparison2urlsoverlap:
    input:
        '{target}.{num}.urlscomparison',
        '{target}.lettr'
    output:
        '{target}.{num}.urlsoverlap'
    shell:
        '{BITEXTOR}/bin/bitextor-urlsetoverlap -l {wildcards.target}.lettr < {input[0]} > {output}'

rule urlsoverlap2rank:
    input:
        '{target}.{num}.urlsoverlap',
        '{target}.lettr'
    output:
        '{target}.{num}.rank'
    shell:
        '{BITEXTOR}/bin/bitextor-rank -m {BITEXTOR}/{MODEL} -w {BITEXTOR}/{WEIGHTS} < {input[0]} > {output}'

rule aligndocumentsparacrawl:
    input:
        '{target}.lett'
    output:
        '{target}.docalign.paracrawl'
    shell:
        'DOCALIGN=$(mktemp {TMPDIR}/docalign.XXXXXX); '
        '{BITEXTOR}/bin/doc_align.sh -f {input} -l {LANG2} -t "{MT_COMMAND}" -d -w $DOCALIGN > {output}'

rule aligndocumentsbitextor:
    input:
        '{target}.1.rank',
        '{target}.2.rank',
        '{target}.lettr'
    output:
        '{target}.docalign.bitextor'
    shell:
        '{BITEXTOR}/bin/bitextor-align-documents -l {input[2]} -n 1 -i converge -r /dev/null {input[0]} {input[1]} > {output}'

rule hunaligndic:
    input:
        expand("{dic}", dic=DIC)
    output:
        '{dir}/hunalign_dic'.format(dir=transient)
    shell:
        'tail -n +2 {input} | sed -r "s/\t/ @ x/g" > {output}'



#================================== SEGMENT ALIGNMENT ==================================#

rule alignsegments:
    input:
        '{dir}/hunalign_dic'.format(dir=transient),
        "{name}.docalign."+"{extension}".format(extension=DOCALIGNEXT)
    output:
        '{name}.segalign'
    shell:
        '{BITEXTOR}/bin/bitextor-align-segments -d {input[0]} -t {TMPDIR} --lang1 {LANG1} --lang2 {LANG2} {USENLTK} < {input[1]} > {output}'

rule concat_segs:
    input:
        expand("{{dir}}/{webdomain}.segalign", webdomain=domain_2_fulldomains.keys())
    output:
        "{dir}/{l1}-{l2}.sent"
    shell:
        "cat {input} > {output}"


rule cleansegments:
    input:
        "{dir}/{l1}-{l2}.sent"
    output:
        "{dir}/{l1}-{l2}.segclean"
    shell:
        '{BITEXTOR}/bin/bitextor-cleantextalign -q {MINQUALITY} -m {MAXLINES} -s < {input} > {output}'

#================================== POST PROCESSING ==================================#

#NOTE: did not add zipporah since it will be deprecated in version 7 of bitextor
#TODO: Add Bicleaner, add deduplication

rule bicleaner:
    input:
        "{dir}/{l1}-{l2}.segclean"
    output:
        "{dir}/{l1}-{l2}.bicleaner.scores"
    shell:
        'python3  {BITEXTOR}/bin/bicleaner_classifier_full.py --threshold {BICLEANER_THRESHOLD} {input} {output} {BICLEANER_CONFIG}'

rule bicleanerfilter:
    input:
        "{dir}/{l1}-{l2}.bicleaner.scores"
    output:
        "{dir}/{l1}-{l2}.bicleaner"
    shell:
        '{BITEXTOR}/bin/bitextor-filterbicleaner --threshold {BICLEANER_THRESHOLD} < {input} > {output}'

rule elrc:
    input:
        "{dir}/{l1}-{l2}."+"{extension}".format(extension=BICLEANER)
    output:
        "{dir}/{l1}-{l2}.elrc"
    shell:
        '{BITEXTOR}/bin/bitextor-elrc-filtering -c "url1,url2,seg1,seg2,hunalign{BICLEANEROPTION}" -s < {input} > {output}'

rule tmx:
    input:
        "{dir}".format(dir=config["transient-dir"])+"/{l1}-{l2}.elrc"
    output:
        "{dir}".format(dir=config["permanent-dir"])+"/{l1}-{l2}.tmx"
    shell:
        "{BITEXTOR}/bin/bitextor-buildTMX --lang1 {LANG1} --lang2 {LANG2} -c url1,url2,seg1,seg2,hunalign{BICLEANEROPTION},lengthratio,numTokensSL,numTokensTL,idnumber < {input} > {output}"
